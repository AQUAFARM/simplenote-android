buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
    }
}

apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    maven { url "http://simperium.github.io/simplenote-android" }
    maven { url "http://wordpress-mobile.github.io/Android-PasscodeLock"}
    maven { url 'http://wordpress-mobile.github.io/WordPress-Android' }
}

dependencies {
    compile "com.simperium.android:simperium:0.6.6"
    compile "com.android.support:appcompat-v7:23.0.1"
    compile 'com.android.support:design:23.0.1'
    compile "com.google.android.gms:play-services-analytics:8.1.0"
    compile "com.google.android.gms:play-services-wearable:8.1.0"
    compile "org.wordpress:passcodelock:1.0.0"
    compile 'com.automattic:tracks:1.0.5'

    wearApp project(':Wear')
}

version "1.3.3"

android {

    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.automattic.simplenote"
        versionName project.version
        versionCode 43
        minSdkVersion 15
        targetSdkVersion 23

        buildConfigField "String", "SIMPERIUM_APP_ID", "\"${project.simperiumAppId}\""
        buildConfigField "String", "SIMPERIUM_APP_KEY", "\"${project.simperiumAppKey}\""
        buildConfigField "String", "HOCKEY_APP_ID", "\"${project.hockeyAppId}\"";
        buildConfigField "String", "GOOGLE_ANALYTICS_ID", "\"${project.googleAnalyticsId}\"";
        buildConfigField "String", "BUILD", "\"${gitDescribe()}\"";
        buildConfigField "String", "BUILD_HASH", "\"${gitHash()}\"";

     }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix '.debug'
        }
    }
	
	lintOptions {
	    checkReleaseBuilds false
	}

    useLibrary 'org.apache.http.legacy'
}

if(["storeFile", "storePassword", "keyAlias", "keyPassword"].count { !project.hasProperty(it) } == 0 ){
    android {
      signingConfigs {
          release {
              storeFile = file(project.storeFile)
              storePassword = project.storePassword
              keyAlias = project.keyAlias
              keyPassword = project.keyPassword
          }
          debug {
              storeFile = file(project.storeFile)
              storePassword = project.storePassword
              keyAlias = project.keyAlias
              keyPassword = project.keyPassword
          }
      }
    }
    android.buildTypes.release.signingConfig = android.signingConfigs.release
}

